* Model developed by Eugene Dvoskin "http://www.audio-perfection.com" 02/05/2012
* It matches most of DC, AC, Transient, Stability and Noise performance of TI TL431
* No attempts were made to cover Temperature dependences
.SUBCKT TL431ED CATHODE ANODE REF
Q1 CATHODE REF N005 QN_ED
R4 N005 N009 3.28k
R2 N009 N012 2.4k
R3 N009 N010 7.2k
Q2 N012 N012 ANODE QN_ED area=1.2
Q3 N010 N012 N014 QN_ED area=2.2
R1 N014 ANODE 800
Q4 N003 N005 N006 QN_ED
R5 N006 N011 4k
Q5 N011 N010 ANODE QN_ED
Q6 N004 N013 ANODE QN_ED area=0.5
Q7 N003 N003 N001 QP_ED
Q8 N004 N003 N002 QP_ED
R7 CATHODE N001 800
R8 CATHODE N002 800
Q9 CATHODE N004 N007 QN_ED
R9 N008 N007 150
Q10 CATHODE N008 ANODE QN_ED area=5
R10 N008 ANODE 10k
Q11 N004 N004 REF QN_ED
D1 ANODE N004 D_ED
R6 N013 N012 1k
D2 ANODE CATHODE D_ED
C1 CATHODE N004 10p
C2 N010 N011 20p
.model QN_ED NPN(BF=140 Cje=1p Cjc=2p Rb=40 VAF=80 VAR=50 KF=3.2e-16 AF=1 BVbe=2. Ibvbe=1p)
.model QP_ED PNP(BF=60 Cje=1p Cjc=3p Rb=80 VAF=70 VAR=40)
.MODEL D_ED D(Rs=5 CJ0=4p)
.ends TL431ED

* PSpice Model Editor - Version 9.2.1
*$
.SUBCKT IXTR170P10P D G S
*********************************************************************
*Note:                                                              *
* Althought models can be a useful tool in evaluating device        *
* performance, they cannot model exact device performance           *
* under all conditions, nor are they intended to replace            *
* bread boarding for final verification. Therefore IXYS does        *
* not assume any liability arsing from their use.  IXYS reserves    *
* the right to change models without prior notice. The Pspice model *
* does not constitute product data sheet. Designer should refer to  *
* the data sheet to guranteed the limit and specification.          *
*********************************************************************
.FUNC Rdson(X1) {limit(0.0007*abs(X1)+0.9429,1,1.5)}
L_ds D D1 3n
R_ds D D1 3
G_ds 9 D1 value={V(9,D)/(Rdson(I(Vsen))*0.0030)}
R_gs G1 70 1.2
L_gs G G1 8n
R_gss G G1 8
L_s SGP S 4n
R_ss SGP S 0.01
Vsen SGP SG 0
R_S S11 SG 0.002
M111 9 70 S11 S11 MM111 L=10u W=240u
.MODEL MM111 PMOS LEVEL=1 
+IS=1e-32
+VTO=-4.0
+LAMBDA=45.88605e-03
+KP=1.00
C_vgs1 70 SG 12.6n
*
X_cdg  9 70 C_dg_170P10P 
X_cds SG D1 C_ds_170P10P 
X_D1 SGP D1 D_170P10P  
*
.ENDS
*$
.SUBCKT C_ds_170P10P Ps_d Ns_s
*.param soft_sw_cds=1
.param CdsAdj=2
*R_ee2 Ps_d Ps_d1 1e-3
G_Cds Ps_d Ns_s value={(V(V_Sel))*I(V_set)*(1e12)}
E_set V_CurrentP V_CurrentN Ps_d Ns_s 1
C_set V_CurrentP 0 1e-12
V_set 0 V_CurrentN 0
E_LowVH LowVH 0 TABLE {abs(V(Ps_d)-V(Ns_s))}=(
+(0,17500e-12)
+(5,6500e-12)
+(10,5230e-12)
+(15,4351e-12)
+(20,3719e-12)
+(25,3260e-12)
+(30,2940e-12)
+(40,2486e-12))
E_Sel V_Sel 0 value={V(LowVH)}
.ENDS
*$
.SUBCKT C_dg_170P10P Ds_d Ds_g
.param soft_sw_cds=1
.param CdsAdj=0
G_Cdg Ds_d Ds_g value={(V(LowV)*(1+CdsAdj))*I(V_set)*(1e12)}
E_set V_CurrentP V_CurrentN Ds_d Ds_g 1
C_set V_CurrentP 0 1e-12
V_set 0 V_CurrentN 0
E_LowVH LowV 0 value={1.0*V(Low_low)}
*E_LowVH LowV 0 value={V(Low_low)+V(High_high)}
E_Low_low Low_low 0 TABLE {abs(V(Ds_D)-V(Ds_g))}=(
+(0,5500e-12)
+(5,2600e-12)
+(10,2070e-12)
+(15,1549e-12)
+(20,1191e-12)
+(25,930e-12)
+(30,740e-12)
+(40,504e-12))
.ENDS
*$
.SUBCKT D_170P10P  D S
**
D_1 S1 Dn BD1
V_S1 S S1 0
V_S2 Dn D 0
L_d VA 0 45n
R_D VA 0 1.5
G_s VA 0 value={1*I(V_S2)}
R_g VA 0 10e6
G_d D S value={5*V(VA)}
R_dd D S 1000e6
.MODEL BD1 D
+ IS=25.363E-8
+ N=5
+ RS=18.7224E-3
+ IKF=366.09E-6
+ M=.62416
+ VJ=2.4801
+ ISR=10.010E-21
+ BV=99.901
+ IBV=25.987E-3
*+ TT=89.465E-9
.ENDS
*$
